apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'

group 'io.github.hoplers'
version '0.0.3'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.awaitility:awaitility:4.0.1'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'io.github.hoplers'
            artifactId 'PartitionedThreadPoolExecutor'
            version '0.0.3'

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'An implmenetaiton of a thread pool that allows tasks to be executed in partitions'
            name 'PartitionedThreadPool4J'
            url 'https://github.com/hoplers/PartitionedThreadPool4J'
            organization {
                name 'io.github.hoplers'
                url 'https://github.com/hoplers'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/hoplers/PartitionedThreadPool4J/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/hoplers/PartitionedThreadPool4J/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/hoplers/PartitionedThreadPool4J'
                connection 'scm:git:git://github.com/hoplers/PartitionedThreadPool4J.git'
                developerConnection 'scm:git:ssh://git@github.com:hoplers/PartitionedThreadPool4J.git'
            }
            developers {
                developer {
                    name 'Harel Opler'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

